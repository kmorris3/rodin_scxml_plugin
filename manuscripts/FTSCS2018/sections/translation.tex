% !TEX root = ../SCXMLREF.tex

\section{\SCXML Translation}
\label{sec:translation}

The translation from \iUMLB to \EventB is based on an abstract `basis' that models the `run to completion' semantics. 
This basis consists of an \EventB \emph{context} and \emph{machine} that are the same for all input models and are refined by the specific output of the translation.  
The basis context, Listing~\ref{lst:BasisContext}, introduces a given set of all possible triggers that is partitioned into internal and external ones, some of which will be introduced in future refinements. 
Refinements partition these trigger sets further to introduce concrete triggers, leaving a new abstract set to represent the remaining triggers yet to be introduced. 
For example, the \IDS model introduces a specific internal trigger, \textbf{spi\_done},  by partitioning |SCXML_FutureInternalTrigger| into the singleton \textbf{\{spi\_done\}} and a new set, |SCXML_FutureInternalTrigger0|, representing the remainder. 
 % as shown in line~\ref{line:refPartition} of Listing~\ref{lst:SecBotCont0}. 

\begin{lstlisting}[caption={Abstract basis context},label={lst:BasisContext}, language=Event-B, escapechar=|, frame=single, basicstyle=\rmfamily\scriptsize]
context
	basis_c 	// (generated for SCXML)
sets
	SCXML_TRIGGER	 // all possible triggers
constants
	SCXML_FutureInternalTrigger	 // all possible internal triggers
	SCXML_FutureExternalTrigger	 // all possible external triggers  
axioms
	partition(SCXML_TRIGGER, SCXML_FutureInternalTrigger, SCXML_FutureExternalTrigger) 
end
\end{lstlisting}	

% \begin{lstlisting}[caption={Context for \IDS abstract model},label={lst:SecBotCont0}, language=Event-B, escapechar=|, frame=single]
% context
% 	IDS_Model_0_ctx //(generated from:/IDS_generated/secbot.scxml)
% extends
% 	basis_c 
% constants
% 	SCXML_FutureInternalTrigger0	
% 	SCXML_FutureExternalTrigger0
% 	spi_done	 	//trigger
% axioms
% 	SCXML_FutureExternalTrigger0=SCXML_FutureExternalTrigger
% 	partition(SCXML_FutureInternalTrigger, SCXML_FutureInternalTrigger0,{spi_done}) |\label{line:refPartition}|
% end
% \end{lstlisting}

The basis machine, part of which is shown in Listing~\ref{lst:BasisMachine}, declares variables that correspond to the triggers present in the queue at any given time, and a flag, |SCXML_uc|, that signals when a run to completion macro-step has been completed (no un-triggered transitions are enabled). 
After initialisation, both trigger queues are empty and |SCXML_uc| is set to |FALSE| so that un-triggered transitions are dealt with. 
The basis machine provides events that describe the generic behaviour of models that follow the run to completion semantics in terms of altering the trigger queues and completion flag.
Since new events introduced in a refinement cannot modify existing variables, all future events generated by translation of the specific \SCXML model, will refine these abstract events.
The abstract event, |SCXML_futureExternalTrigger| represents the raising of an external trigger.    
The abstract event, |SCXML_futureInternalTransitionSet| represents a combination of transitions that are triggered by an internal trigger. 
The guards of this event ensure prior completion of the previous macro-step. 
A similar event, |SCXML_futureExternalTransitionSet| (not shown) represents a combination of transitions that are triggered by an external trigger and has the additional guard that the internal trigger queue is empty.
These two triggered transition events reset the completion flag to ensure that any un-triggered transitions that may have become enabled have a chance to fire next.
The abstract event |SCXML_futureUntriggeredTransitionSet| represents a combination of transitions that are un-triggered and may only be fired when the completion flag is unset (FALSE).
It leaves the completion flag unset in case further combinations of un-triggered transitions are enabled.
All three of these transition events also allow for raising a non-deterministic set of internal triggers.
A final abstract event, |SCXML_completion|, sets the completion flag (TRUE) if it is not already set. At this abstract basis level, this is non-deterministically fired since we do not yet have any detail of what needs to be completed.

\begin{lstfloat}[!tb]
\begin{lstlisting}[caption={Abstract basis machine (part of)}, label={lst:BasisMachine},language=Event-B, escapechar=|, frame=single, basicstyle=\rmfamily\scriptsize]
machine 
	basis_m   // (generated for SCXML)
sees 
	basis_c 
variables
	SCXML_iq	  // internal trigger queue
	SCXML_eq	  // external trigger queue
	SCXML_uc	  // run to completion flag
invariants
	SCXML_iq ⊆ SCXML_FutureInternalTrigger	// internal trigger queue
	SCXML_eq ⊆ SCXML_FutureExternalTrigger	// external trigger queue
	SCXML_iq ∩ SCXML_eq= ∅					// queues are disjoint
	SCXML_uc ∈ BOOL							// completion flag
events

	INITIALISATION: 
	begin
		SCXML_iq := {}		//internal Q is initially empty
		SCXML_eq := {}		//external Q is initially empty
		SCXML_uc := FALSE	//completion is initially FALSE
	end

	SCXML_futureExternalTrigger: 
	any SCXML_raisedTriggers where
		SCXML_raisedTriggers ⊆ SCXML_FutureExternalTrigger 
	then
		SCXML_eq ≔ SCXML_eq ∪ SCXML_raisedTriggers 
	end

	SCXML_futureInternalTransitionSet: 
	any SCXML_it SCXML_raisedTriggers where
		SCXML_it ∈ SCXML_iq 
		SCXML_uc = TRUE 
		SCXML_raisedTriggers ⊆ SCXML_FutureInternalTrigger 
	then
		SCXML_uc ≔ FALSE 
		SCXML_iq ≔ (SCXML_iq ∪ SCXML_raisedTriggers) ∖ {SCXML_it} 
	end

	SCXML_futureUntriggeredTransitionSet: 
	any SCXML_raisedTriggers where
		SCXML_uc = FALSE
		SCXML_raisedTriggers ⊆ SCXML_FutureInternalTrigger
	then
		SCXML_uc ≔ FALSE 
		SCXML_iq ≔ SCXML_iq ∪ SCXML_raisedTriggers 
	end

end
\end{lstlisting}
\end{lstfloat}

The translation of a specific \SCXML model comprises two stages as follows. 
Firstly, all possible combinations of transitions that can fire together are calculated and corresponding events are generated, at appropriate refinement levels, that refine the abstract basis events.  
If these transitions raise internal triggers, a guard, (e.g. |{i1,i2...} <: SCXML_raisedTrigger|, where |i1,i2..| have been added to the internal triggers set), is introduced that defines the raised triggers parameter. 
The subset constraint leaves it open for more raised triggers to be added by later refinements.
For triggered transition combinations, the trigger is specified in a guard (see line~\ref{line:defTrigger} of Listing~\ref{lst:SecBotMach0}) that provides a value for the trigger parameter. 

\begin{lstlisting}[caption={Event-B event corresponding to internal triggered transition to \textbf{Wait50ms} state in refinement level 1 shown in Fig.~\ref{fig:ASIC}}, label={lst:SecBotMach0},language=Event-B, escapechar=|, frame=single, float=t]
spi_done__InitialiseSensor_Wait50ms:	
refines SCXML_futureInternalTransitionSet 
any SCXML_it SCXML_raisedTriggers where
	SCXML_it  ∈ SCXML_iq 
	SCXML_uc = TRUE
	SCXML_raisedTriggers ⊆ SCXML_FutureInternalTrigger
	InitialiseSensor = TRUE
	SCXML_it = spi_done  	//trigger for this transition |\label{line:defTrigger}|
then
	SCXML_uc ≔ FALSE
	SCXML_iq ≔ (SCXML_iq ∪ SCXML_raisedTriggers) ∖ {SCXML_it}
	InitialiseSensor ≔ FALSE
	Wait50ms ≔ TRUE
end
\end{lstlisting}

Secondly, the \SCXML state-chart is translated into a corresponding iUML-B state-machine whose transitions elaborate (i.e. add state change details to) the possible transition combination events that the transition may be involved in. 
A transition may fire in parallel with transitions of parallel nested state-machines that have the same (possibly null) trigger.
Fig.~\ref{fig:iumlb-verif} shows the generated \iUMLB first refinement level corresponding to the \IDS described in Fig.~\ref{fig:ASIC_SPI_1}. 
Table~\ref{tab:translation_rules} provides a summary of the main SCXML to iUML-B/Event-B translation rules.
The iUML-B state-machine is subsequently translated into Event-B using the standard iUML-B translation~\cite{snook14:_b_statem} which provides variables to model the current state and guards and actions to model the state changes that transitions perform..


\begin{EventBNoShortInline}
  \begin{table}[]
	\centering
	\begin{tabular}{@{}p{0.25\linewidth}p{0.4\linewidth}p{0.35\linewidth}@{}}
		\hline
		\textbf{SCXML feature} & \textbf{Generated Event-B} & \textbf{Notes} 
		\\\midrule
		Top level scxml model &
		A refinement chain of Event-B machines each containing an initialisation event and a root level iUML-B state-machine &
		The depth of the refinement chain is found by searching the scxml for the maximum refinement annotation
		\\\hline
%		Invariant owned by the top level scxml & 
%		Invariant owned by an Event-B machine produced from the containing scxml &
%		Added only at the refinement level defined in the invariant (default 0) 
%		\\\hline
		State not owned by a parallel & 
		State owned by the iUML-B state-machine that has been produced from the containing scxml or state &
		A refined state is also added in all of the refinements of the parent iUML-B state-machine 
		\\\hline
		Invariant owned by a state that generates an iUML-B state (i.e. not contained in a parallel). &
		Invariant owned by the iUML-B state that has been produced from the containing scxml state. &
		Added only at the refinement level defined in the invariant (defaults to first level at which containing iUML-B state is introduced)
		\\\hline
		State owned by a parallel element &
		An iUML-B state-machine is added to the state that has been generated from the owner of the parallel &
		The nested iUML-B state-machine is added starting from the refinement level that is annotated on the source state and continuing throughout subsequent refinements.
		\\\hline
		State that contains states &
		A nested iUML-B state-machine, with an initial state, is added to the iUML-B state that has been produced from the source state, if any, or from its containing state if it did not produce an iUML-B state. &
		The nested iUML-B state-machine is added starting from the refinement level that is annotated on the source state and continuing throughout subsequent refinements.
		\\\hline
		Initial Attribute of a top-level scxml model &
		An iUML-B initial state, and a transition from it to the iUML-B state indicated in the scxml initial attribute, are added to the iUML-B state-machine produced from the parent scxml &
		The iUML-B initial state and iUML-B transition are added at all refinement levels. The iUML-B transitions are set to elaborate the Event-B INITIALISATION event for that refinement level.
		\\\hline
		Final &
		An iUML-B state with a transition to a final state are added to the state-machine that has been generated from the containing scxml or state. The transition represents the same events that are linked to the transitions that exit the parent iUML-B state. &
		The iUML-B state, final state and transition are also added as refined elements to all of the refinements of the parent iUML-B state-machine
		\\\hline
		Transition &
		An iUML-B transition is added to the state-machine that has been generated from the containing scxml or state. The iUML-B transition’s source and target are those that have been produced from the corresponding scxml transition’s source and target states. 
		The transition elaborates generated Event-B events according to the rules given in Section ~\ref{sec:translation} &
		The iUML-B transition and elaborated Event-B events are also added as corresponding refined elements in all of the refinements of the parent iUML-B state-machine
%		\\\hline
%		Target attribute of a transition element &
%		Used to determine the transitions target state as described above. &
		\\\hline																	
	\end{tabular}
	\caption{Main SCXML to iUML-B/Event-B Translation Rules}
	\label{tab:translation_rules}
  \end{table}
\end{EventBNoShortInline}

A tool to automatically translate \SCXML models into \iUMLB has been produced. 
The tool is based on the \EMF and uses an \SCXML meta-model provided by Sirius~\cite{siriuswebsite} which has good support for extensibility. 
%The tooling for \iUMLB and \EventB already contains \EMF meta-models and provides a generic translator framework which has been specialised for the \SCXML to \iUMLB translation. 

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../SCXMLREF"
%%% End:
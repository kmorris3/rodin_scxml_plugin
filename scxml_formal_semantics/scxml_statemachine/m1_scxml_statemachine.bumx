machine m1_scxml_statemachine
refines m0_scxml_statemachine
 
/*
 * This machine represents the refinement of a joined scxml and statemachine model
 * 
 * Since we need the refinement stuff from the scxml and statemachines... 
 * it has to use m1_scxml and m1_statemachine
 */
  
includes m1_scxml
includes m1_statemachine
//includes cnc_scxml_statemachine

sees gluing_scxml_statemachine_ctx c0_4_tree_prop
 
events
	event INITIALISATION
	synchronises INITIALISATION
	then
		@update-active: cnc_active ≔ cnc_init
		@update-active_leaf: cnc_active_leaf ≔ cnc_init_leaf
	end

	event old_raiseExternalTrigger refines abs_raiseExternalTrigger
	synchronises old_raiseExternalTrigger
	end
	
	event new_raiseExternalTrigger
	synchronises new_raiseExternalTrigger
	end
	
	event old_dequeueExternalTrigger refines abs_dequeueExternalTrigger
	synchronises old_dequeueExternalTrigger
	end
	
	event new_dequeueExternalTrigger
	synchronises new_dequeueExternalTrigger
	end
	
	event old_dequeueInternalTrigger refines abs_dequeueInternalTrigger
	synchronises old_dequeueInternalTrigger
	end
	
	event new_dequeueInternalTrigger
	synchronises new_dequeueInternalTrigger
	end
	
	event old_triggeredTransition refines abs_triggeredTransition
	synchronises old_triggeredStep
	synchronises old_transition
		where
		@is_triggered:	cnc_transition ∈ dom(cnc_triggeredTransitions)
		@this_trigger:		cnc_triggeredTransitions(cnc_transition) = cnc_trigger 
	end
	
	event new_triggeredTransition refines abs_futureStep
	synchronises new_triggeredStep
	synchronises new_transition
		where
		@is_triggered:	cnc_transition ∈ dom(cnc_triggeredTransitions)
		@this_trigger:		cnc_triggeredTransitions(cnc_transition) = cnc_trigger 
	end
	
	event old_discardTrigger refines abs_discardTrigger
	synchronises old_discardTrigger
	where
		@finalisedFired: cnc_source[cnc_finalisedTransitions] ∩ cnc_active = ∅
	end
	
	event new_discardTrigger
	synchronises new_discardTrigger
	where
		@finalisedFired: cnc_source[cnc_finalisedTransitions] ∩ cnc_active = ∅
	end
	
	event old_untriggeredTransition refines abs_untriggeredTransition
	synchronises old_untriggeredStep
	synchronises old_transition
		where 
		@not_triggered: 	cnc_transition ∉ dom(cnc_triggeredTransitions)
	end
	
	event new_untriggeredTransition refines abs_futureStep
	synchronises new_untriggeredStep
	synchronises new_transition
		where 
		@not_triggered: 	cnc_transition ∉ dom(cnc_triggeredTransitions)
	end
	
	event old_completion refines abs_completion
	synchronises old_completion
		where
		@finalisedFired: cnc_source[cnc_finalisedTransitions] ∩ cnc_active = ∅
	end
	
	event new_completion
	synchronises new_completion
	end
	
	event cnc_futureStep refines abs_futureStep	//needed if we allow adding raising of abstract internal triggers in later refinements
	synchronises cnc_futureStep
	end

end
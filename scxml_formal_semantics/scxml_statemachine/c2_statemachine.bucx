context c2_statemachine
extends c1_statemachine
 
constants
	refined_states // The set of states
	refined_root // The root state
	refined_container // The container relationship between states
	
	refined_leaves // The set of leaf states
	refined_transitions // The set of transitions
	refined_source // The source of each transition
	refined_target // The target of each transition

	refined_init // The initial states
	refined_init_leaf // The initial leaf
	
	transition_link
axioms
	@refined_statemachine: refined_states ↦ refined_root ↦ refined_container ↦
	                       refined_transitions ↦ refined_source ↦ refined_target ∈ STATE_MACHINE
	// The set of leaf states are those in @states and
	// does not contain any other state.
	@def-refined_leaves: refined_leaves = refined_states ∖ ran(refined_container)

	@typeof-refined_init: refined_init ⊆ refined_states
	@typeof-refined_init_leaf: refined_init_leaf ∈ refined_leaves
	@refined_init-closure: refined_init = cl(refined_container)[{refined_init_leaf}] ∪ {refined_init_leaf}
	
	// Linking refined_statemachine and statemachine
	@linking-states: states ⊆ refined_states
	@linking-root: root = refined_root
	@linking-container: container ⊆ refined_container
	@linking-transitions: transition_link ∈ refined_transitions ⇸ transitions // abstract transitions can be removed
	@linking-source_old_transition: ∀rt · rt ∈ dom(transition_link) ⇒ // @rt is a refined transition with an abstract counterpart.
	  	source(transition_link(rt)) ∈ cl(refined_container)[{source(rt)}] ∪ {source(rt)}
	  	// The refined source can be more deterministic than abstract source
	  	// That is the source of the abstract transition is either the same as 
	  	// that of the refined transition or it is an ancestor of that. 
	@linking-target_old_transition: ∀rt · rt ∈ dom(transition_link) ⇒
	  	target(transition_link(rt)) ∈ cl(refined_container)[{target(rt)}] ∪ {target(rt)}
	@linking-source_target_new_transition: ∀rt · rt ∉ dom(transition_link) ⇒ 
		cl(refined_container)[{source(rt)}] ∩ states = cl(refined_container)[{target(rt)}] ∩ states
	  	
end
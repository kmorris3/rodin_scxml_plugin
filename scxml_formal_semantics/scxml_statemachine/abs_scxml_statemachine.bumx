machine abs_scxml_statemachine

/*
 * This machine provides a renamed abstract version of a joined scxml and statemachine model.
 * 
 * Idealing this would just rename an instantiation of the already joined scxml_statemachine.
 * However, since there are some issues with renamed inclusion, for now we take an alternative 
 * route of re-joining the already renamed scxml and statemachine models
 * 
 * 
 */
 
includes abs_scxml 
includes abs_statemachine
 
sees abs_scxml_statemachine_ctx c0_4_tree_prop
 
events
	event INITIALISATION
	synchronises INITIALISATION
	//synchronises INITIALISATION	// cannot synch since both events have the same name hence...
	then
		@update-active: abs_active ≔ abs_init					//copied from scxml_statemachine
		@update-active_leaf: abs_active_leaf ≔ abs_init_leaf	//copied from scxml_statemachine
	end

	event abs_raiseExternalTrigger
	synchronises abs_raiseExternalTrigger
	end
	
	event abs_dequeueExternalTrigger
	synchronises abs_dequeueExternalTrigger
	end
	
	event abs_dequeueInternalTrigger
	synchronises abs_dequeueInternalTrigger
	end
	
	event abs_triggeredTransition
	synchronises abs_triggeredStep
	synchronises abs_transition
	where 
		@is_triggered:	abs_transition ∈ dom(abs_triggeredTransitions)
		@this_trigger:		abs_triggeredTransitions(abs_transition) = abs_trigger 
	end
	
	event abs_discardTrigger
	synchronises abs_discardTrigger
	where
		@finalisedFired: abs_source[abs_finalisedTransitions] ∩ abs_active = ∅
	end
	
	event abs_untriggeredTransition
	synchronises abs_untriggeredStep
	synchronises abs_transition
	where 
		@not_triggered: 	abs_transition ∉ dom(abs_triggeredTransitions)
	end
	
	event abs_completion
	synchronises abs_completion
	where
		@finalisedFired: abs_source[abs_finalisedTransitions] ∩ abs_active = ∅
	end
	
	event abs_futureStep	//needed if we allow adding raising of abstract internal triggers in later refinements
	synchronises abs_futureStep
	end

end
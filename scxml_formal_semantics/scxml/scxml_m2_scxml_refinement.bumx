machine scxml_m2_scxml_refinement
refines scxml_m1_scxml_refinement
sees scxml_c1_refined
variables
	iq_r
	eq_r
	dt_r
	uc_r
events
	event INITIALISATION
	then
		@iq_r_init:	iq_r ≔ ∅ 
		@eq_r_init: 	eq_r ≔ ∅
		@dt_r_init:	dt_r ≔ ∅
		@uc_r_init: 	uc_r ≔ FALSE
	end

	event raiseExternalTrigger refines raiseOldExternalTrigger raiseNewExternalTrigger
	any t where
		@t:			t ∈ RefinedExternalTriggers
	then
		@eq_set:		eq_r ≔ eq_r ∪ {t}
	end

	event dequeueExternal refines dequeueOldExternal dequeueNewExternal
	any t where 
		@uc:			uc_r = TRUE
		@t: 			t ∈ eq_r
		@iq:			iq_r = ∅
	then 
		@dt_set: 	dt_r ≔ {t}
		@eq_set:		eq_r ≔ eq_r ∖ {t} 
		@uc_set:		uc_r ≔ FALSE
	end

	event dequeueInternal refines dequeueOldInternal dequeueNewInternal
	any t where 
		@uc:			uc_r = TRUE
		@t: 			t ∈ iq_r
	then 
		@dt_set: 	dt_r ≔ {t}
		@eq_set:		iq_r ≔ iq_r ∖ {t} 
		@uc_set:		uc_r ≔ FALSE
	end

	event triggeredTransition refines oldTriggeredTransition newTriggeredTransition
	any t R1
	where 
		@t: 			t ∈ dt_r
		@R:			R1 ⊆ RefinedInternalTriggers 	
	then 
		@dt_set: 	dt_r ≔ ∅
		@iq_set:		iq_r ≔ iq_r ∪ R1		//raised triggers
	end

	event completeTriggered refines oldCompleteTriggered newCompleteTriggered
		any t where 
		@t: 			t ∈ dt_r
	then
		@dt_set: 	dt_r ≔ ∅
	end

	event untriggeredTransition refines oldUntriggeredTransition newUntriggeredTransition
	any R1
	where 
		@dt: 		dt_r = ∅
		@uc:			uc_r = FALSE				//this one is needed!!
		@R:			R1 ⊆ RefinedInternalTriggers
	then 
		@iq_set:		iq_r ≔ iq_r ∪ R1		//raised triggers
	end

	event completeUntriggered refines oldCompleteUntriggered newCompleteUntriggered
	where
		@dt: 		dt_r = ∅
		@uc:			uc_r = FALSE			//this one is needed!!
	then
		@uc_set:		uc_r ≔ TRUE
	end

	event futureTransition extends futureTransition
	end

end
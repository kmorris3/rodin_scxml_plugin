machine scxml_m0_scxml 
sees scxml_c0_abstract

variables
	iq
	eq
	dt
	uc

invariants
	@iq: iq ∈ ℙ(AbstractInternalTriggers)
	@eq: eq ∈ ℙ(AbstractExternalTriggers)
	@dt: dt ∈ DeQueueType
	@dtwd: dt ⊆ AbstractInternalTriggers ∪ AbstractExternalTriggers
	@uc: uc ∈ BOOL
	
	@firingTriggered: dt ≠ ∅ ⇒ uc=FALSE		//just a sanity check
	theorem @readyToDequeue: uc=TRUE ⇒ dt = ∅		//just a sanity check
		
events

	event INITIALISATION
	then
		@iq_init:	iq ≔ ∅ 
		@eq_init: 	eq ≔ ∅
		@dt_init:	dt ≔ ∅
		@uc_init: 	uc ≔ FALSE
	end
	
	event raiseExternalTrigger
	any t where
		@t:			t ∈ AbstractExternalTriggers
	then
		@eq_set:		eq ≔ eq ∪ {t}
	end
	
	event dequeueExternal
	any t where 
		@uc:			uc = TRUE
		theorem @dt:		dt = ∅	//sanity check
		@t: 			t ∈ eq
		@iq:			iq = ∅
	then 
		@dt_set: 	dt ≔ {t}
		@eq_set:		eq ≔ eq ∖ {t}
		@uc_set:		uc ≔ FALSE
	end
	
	event dequeueInternal
	any t where 
		@uc:			uc = TRUE
		theorem @dt:		dt = ∅	//sanity check
		@t: 			t ∈ iq
	then 
		@dt_set: 	dt ≔ {t}
		@eq_set:		iq ≔ iq ∖ {t} 
		@uc_set:		uc ≔ FALSE
	end
	
	event triggeredTransition 
	any t R
	where 
		@t: 			t ∈ dt
		theorem @uc:		uc = FALSE	//sanity check
		@R:			R ⊆ AbstractInternalTriggers
	then 
		@dt_set: 	dt ≔ ∅
		@iq_set:		iq ≔ iq ∪ R		//raised triggers
	end
	
	event completeTriggered
		any t where 
		@t: 			t ∈ dt
		theorem @uc:		uc = FALSE	//sanity check			 
	then 
		@dt_set: 	dt ≔ ∅
	end
	
	event untriggeredTransition
	any R
	where 
		@dt: 		dt = ∅
		@uc:			uc = FALSE		
		@R:			R ⊆ AbstractInternalTriggers
	then 
		@iq_set:		iq ≔ iq ∪ R		//raised triggers
	end
	
	event completeUntriggered
	where
		@dt: 		dt = ∅
		@uc:			uc = FALSE
	then
		@uc_set:		uc ≔ TRUE
	end
	
//	event futureDequeue	//needed to allow for future triggers starting a run
//	where 
//		@dt: 		dt = ∅
//		@uc:			uc = TRUE		
//	then 
//		@iq_set:		uc ≔ FALSE	
//	end
	
	event futureTransition	//needed if we allow adding raising of abstract internal triggers in later refinements
	any R
	where 
		@R:			R ⊆ AbstractInternalTriggers
		@uc:			uc = TRUE		
	then 
		@iq_set:		iq ≔ iq ∪ R		//raised triggers
	end
	
end


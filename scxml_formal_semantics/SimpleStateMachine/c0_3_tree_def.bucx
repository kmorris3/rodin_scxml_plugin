/*
 * This context defines the notion of trees on a (sub-)set of states
 */
context c0_3_tree_def
extends c0_1_closure_def

constants
	Tree
axioms
	/*
	 * For each set @Sts, the set of trees on @Sts is the set of tuples 
	 * from root @rt to the parent relation @prn satisfying:
	 * - @rt is in @Sts
	 * - every node except @rt has a parent in @Sts
	 * - @rt is an ancestor of all other nodes in @Sts via @prn
	 */
	@def-Tree: Tree = (
		λ Sts · Sts ⊆ STATE ∣ {
			rt ↦ prn ∣
			rt ∈ Sts ∧
			prn ∈ Sts ∖ {rt} → Sts ∧
			( ∀ n · n ∈ Sts ∖ {rt} ⇒ rt ∈ cl(prn)[{n}])
		}
	)

	theorem @typeof-Tree: Tree ∈ ℙ(STATE) → ℙ(STATE × (STATE ↔ STATE))
	theorem @func-Tree: ∀ Sts · Tree(Sts) = {
			rt ↦ prn ∣
			rt ∈ Sts ∧
			prn ∈ Sts ∖ {rt} → Sts ∧
			( ∀ n · n ∈ Sts ∖ {rt} ⇒ rt ∈ cl(prn)[{n}])
		}
end
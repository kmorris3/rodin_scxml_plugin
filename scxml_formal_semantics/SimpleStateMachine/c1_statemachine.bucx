/*
 * This context declare the signature of a statemachine
 */
context c1_statemachine
extends c0_3_tree_def c0_2_closure_prop

constants
	states // The set of states
	root // The root state
	container // The container relationship between states
	
	leaves // The set of leaf states
	transitions // The set of transitions

	init // The initial states
	init_leaf // The initial leaf
axioms
	// @states is a set of states
	@typeof-states: states ⊆ STATE
	
	// @root and @container form a tree on @states
	@tree_structure: root ↦ container ∈ Tree(states)
	
	// Derivable properties from definition of Tree
	theorem @typeof-root: root ∈ states
	theorem @typeof-container: container ∈ states ∖ {root} → states
	theorem @root-reachable: ∀ state · state ∈ states ∖ {root} ⇒ root ∈ cl(container)[{state}]
	
	// The set of leaf states are those in @states and
	// does not contain any other state.
	@def-leaves: leaves = states ∖ ran(container)
	theorem @leaves_are_states: leaves ⊆ states
  	// Copy for automatic proofs
  	theorem @thm12-range_preservation: ∀r· ran(r) = ran(cl(r))
	theorem @range-preservation: ran(container) = ran(cl(container))
	
	// Transitions are relation between leaf states
	@typeof-transitions: transitions ∈ leaves ↔ leaves
	
	@typeof-init: init ⊆ states
	@typeof-init_leaf: init_leaf ∈ leaves
	@init-closure: init = cl(container)[{init_leaf}] ∪ {init_leaf}
end